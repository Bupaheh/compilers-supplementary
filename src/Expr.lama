-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[st, w], expr) {
  -- printf("%s %s %s\n", st.string, w.string, expr.string);
  case expr of
    Var (s) -> [c, st (s)]
  | Const (v) -> [c, v]
  | Binop (op, a, b) -> 
      case evalList (c, {a, b}) of
        [c, {a, b}] -> [c, evalOp (op, a, b)]
      esac
  | Ref (s) -> [c, RefVal (s)]
  | Skip -> [c, Bottom]
  | Assn (s, ex) ->
      case evalList (c, {s, ex}) of
        [[st, w], {RefVal (s), ex}] -> [[st <- [s, ex], w], ex]
      esac
  | Read (s) ->
      case readWorld (w) of
        [n, w] -> [[st <- [s, n], w], Bottom]
      esac
  | Write (ex) -> 
      case eval (c, ex) of
        [[st, w], ex] -> [[st, writeWorld (ex, w)], Bottom]
      esac
  | Seq (s1, s2) -> 
      case evalList (c, {s1, s2}) of
        [c, {_, s2}] -> [c, s2]
      esac
  | If (ex, t, f) ->
      case eval (c, ex) of
        [c, 0] -> eval (c, f)
      | [c, _] -> eval (c, t)
      esac
  | While (ex, s) ->
      case eval (c, ex) of
        [c, 0] -> [c, Bottom]
      | [c, _] -> eval (c, Seq (s, While (ex, s)))
      esac
  | DoWhile (s, ex) -> eval (c, Seq (s, While (ex, s)))
  | Ignore  (ex) -> [eval (c, ex).fst, Bottom]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
