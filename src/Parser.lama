-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun exprHelper(op) {
  [s (op), fun (l, s, r) { Binop (op, l, r) }]
}

-- Primary expression
var primary = memo $ eta syntax (
      x=decimal {Const (stringInt (x))} |
      x=lident  {Var (x)}               |
      inbr[s("("), exp, s(")")]
    ),
    exp = memo $ eta expr ({
        [Left, { exprHelper ("!!") }],
        [Left, { exprHelper ("&&") }],
        [Nona, {
          exprHelper ("=="),
          exprHelper ("!="),
          exprHelper ("<"),
          exprHelper ("<="),
          exprHelper (">"),
          exprHelper (">=")
        }],
        [Left, {
          exprHelper ("+"),
          exprHelper ("-")
        }],
        [Left, {
          exprHelper ("*"),
          exprHelper ("/"),
          exprHelper ("%")
        }]
      }, 
      primary
    );

var ifHelper = memo $ eta syntax (
  kElif cond=exp kThen t=stmts f=ifHelper { If (cond, t, f) } |
  -kElse stmts -kFi |
  kFi { Skip }
);

var stmt = memo $ eta syntax (
  kSkip { Skip } |
  l=lident s[":="] r=exp { Assn (l, r) } |
  kWrite l=inbr[s("("), exp, s(")")] { Write (l) } |
  kRead l=inbr[s("("), lident, s(")")] { Read (l) } |
  kIf cond=exp kThen t=stmts f=ifHelper { If (cond, t, f) } |
  kWhile cond=exp kDo s=stmts kOd { While (cond, s) } |
  kDo s=stmts kWhile cond=exp kOd { DoWhile (s, cond) } |
  kFor s1=stmts s[","] cond=exp s[","] s2=stmts kDo s3=stmts kOd { Seq (s1, While (cond, Seq (s3, s2))) }
);

var stmts = memo $ eta syntax (
  l=stmt s[";"] r=stmts { Seq (l, r) } |
  stmt
);

-- Public top-level parser
public parse = stmts;